SECTION = "x11"

DESCRIPTION = "An open source implementation of the OpenGL spec"
HOMEPAGE = "http://mesa3d.org"
LICENSE = "MIT"

INC_PR = "r11"
PE = "2"

PROTO_DEPS = "xf86driproto glproto"
LIB_DEPS = "virtual/libx11 libxext libxxf86vm libxdamage libxfixes"

DEPENDS = "${PROTO_DEPS}  ${LIB_DEPS}"

SRC_URI = "ftp://ftp.freedesktop.org/pub/mesa/${PV}/MesaLib-${PV}.tar.bz2;name=archive \
           ftp://ftp.freedesktop.org/pub/mesa/${PV}/MesaDemos-${PV}.tar.bz2;name=demos \
          "
S = "${WORKDIR}/Mesa-${PV}"

PROVIDES = "virtual/libgl"

inherit autotools pkgconfig

EXTRA_OECONF = "--enable-glu \
                --disable-glw \
                --disable-glut \
	       "

inherit glx-use-tls

# Package contents vary according to ${MACHINE_DRI_MODULES}.
PACKAGE_ARCH = "${MACHINE}"

PACKAGES =+ "libegl libegl-dev libegl-dbg libglu libglu-dev libosmesa libosmesa-dev libgl libgl-dev ${PN}-xprogs"
FILES_${PN} += "${libdir}/dri/*.so"
FILES_${PN}-xprogs = "${bindir}/glxdemo ${bindir}/glxgears ${bindir}/glxheads ${bindir}/glxinfo"
FILES_libegl = "${libdir}/libEGL.so.* ${libdir}/egl/*.so"
FILES_libgl = "${libdir}/libGL.so.*"
FILES_libglu = "${libdir}/libGLU.so.*"
FILES_libosmesa = "${libdir}/libOSMesa.so.*"

FILES_libegl-dev = "${libdir}/libEGL.* ${includedir}/EGL"
FILES_libgl-dev = "${libdir}/libGL.* ${includedir}/GL"
FILES_libglu-dev = "${libdir}/libGLU.* ${includedir}/GL/glu*.h"
FILES_libosmesa-dev = "${libdir}/libOSMesa.* ${includedir}/osmesa.h"

FILES_${PN}-dbg += "${libdir}/dri/.debug/*"
FILES_libegl-dbg += "${libdir}/egl/.debug/*"

do_stage() {
        autotools_stage_all
		install -d ${STAGING_INCDIR}/GL/	
		#LocalChange: fix cp portability
		cp -pPR ${S}/include/GL/internal* ${STAGING_INCDIR}/GL/
		#cp -pPr ${S}/include/GL/internal* ${STAGING_INCDIR}/GL/
}

NATIVE_INSTALL_WORKS = "1"
do_install_append () {
    install -d ${D}/${bindir}
    for i in glxdemo glxgears glxheads glxinfo; do 
        install -m 0755 ${S}/progs/xdemos/${i} ${D}/${bindir}
    done
    install -d ${D}/${includedir}/GL
    #LocalChange: fix cp portability
    cp -pPR ${S}/include/GL/internal* ${D}/${includedir}/GL/
    #cp -pPr ${S}/include/GL/internal* ${STAGING_INCDIR}/GL/
}
